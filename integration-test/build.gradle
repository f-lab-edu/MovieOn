plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'idea'
}

sourceSets {
    integrationTest {
        java.srcDir "src/integrationTest/java"
        resources.srcDir "src/integrationTest/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

dependencies {
    integrationTestImplementation project(":app")

    integrationTestImplementation "com.h2database:h2"
    integrationTestImplementation "com.navercorp.fixturemonkey:fixture-monkey-starter:0.3.5"
    integrationTestImplementation "org.springframework.security:spring-security-test"
    integrationTestImplementation "org.springframework.boot:spring-boot-starter-web"
    integrationTestImplementation "org.springframework.boot:spring-boot-starter-test"
}

bootJar {
    enabled = false
}

task integrationTest(type: Test) {
    group = 'verification'

    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

processIntegrationTestResources.duplicatesStrategy DuplicatesStrategy.INCLUDE

check.dependsOn integrationTest
